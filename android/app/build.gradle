apply plugin: "com.android.application"

import com.android.build.OutputFile

/**
 * The react.gradle file registers a task for each build variant (e.g. bundleDebugJsAndAssets
 * and bundleReleaseJsAndAssets).
 * These basically call `react-native bundle` with the correct arguments during the Android build
 * cycle. By default, bundleDebugJsAndAssets is skipped, as in debug/dev mode we prefer to load the
 * bundle directly from the development server. Below you can see all the possible configurations
 * and their defaults. If you decide to add a configuration block, make sure to add it before the
 * `apply from: "../../node_modules/react-native/react.gradle"` line.
 *
 * project.ext.react = [
 *   // the name of the generated asset file containing your JS bundle
 *   bundleAssetName: "index.android.bundle",
 *
 *   // the entry file for bundle generation
 *   entryFile: "index.android.js",
 *
 *   // whether to bundle JS and assets in debug mode
 *   bundleInDebug: false,
 *
 *   // whether to bundle JS and assets in release mode
 *   bundleInRelease: true,
 *
 *   // whether to bundle JS and assets in another build variant (if configured).
 *   // See http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Build-Variants
 *   // The configuration property can be in the following formats
 *   //         'bundleIn${productFlavor}${buildType}'
 *   //         'bundleIn${buildType}'
 *   // bundleInFreeDebug: true,
 *   // bundleInPaidRelease: true,
 *   // bundleInBeta: true,
 *
 *   // whether to disable dev mode in custom build variants (by default only disabled in release)
 *   // for example: to disable dev mode in the staging build type (if configured)
 *   devDisabledInStaging: true,
 *   // The configuration property can be in the following formats
 *   //         'devDisabledIn${productFlavor}${buildType}'
 *   //         'devDisabledIn${buildType}'
 *
 *   // the root of your project, i.e. where "package.json" lives
 *   root: "../../",
 *
 *   // where to put the JS bundle asset in debug mode
 *   jsBundleDirDebug: "$buildDir/intermediates/assets/debug",
 *
 *   // where to put the JS bundle asset in release mode
 *   jsBundleDirRelease: "$buildDir/intermediates/assets/release",
 *
 *   // where to put drawable resources / React Native assets, e.g. the ones you use via
 *   // require('./image.png')), in debug mode
 *   resourcesDirDebug: "$buildDir/intermediates/res/merged/debug",
 *
 *   // where to put drawable resources / React Native assets, e.g. the ones you use via
 *   // require('./image.png')), in release mode
 *   resourcesDirRelease: "$buildDir/intermediates/res/merged/release",
 *
 *   // by default the gradle tasks are skipped if none of the JS files or assets change; this means
 *   // that we don't look at files in android/ or ios/ to determine whether the tasks are up to
 *   // date; if you have any other folders that you want to ignore for performance reasons (gradle
 *   // indexes the entire tree), add them here. Alternatively, if you have JS files in android/
 *   // for example, you might want to remove it from here.
 *   inputExcludes: ["android/**", "ios/**"],
 *
 *   // override which node gets called and with what additional arguments
 *   nodeExecutableAndArgs: ["node"],
 *
 *   // supply additional arguments to the packager
 *   extraPackagerArgs: []
 * ]
 */

project.ext.react = [
    entryFile: "index.js"
]

project.ext.envConfigFiles = [
  dev: '.env.staging',
  main: '.env.production',
  nightly: '.env.production'
]

apply from: "../../node_modules/react-native/react.gradle"
apply from: "../../node_modules/react-native-sentry/sentry.gradle"

/**
 * Set this to true to create two separate APKs instead of one:
 *   - An APK that only works on ARM devices
 *   - An APK that only works on x86 devices
 * The advantage is the size of the APK is reduced by about 4MB.
 * Upload all the APKs to the Play Store and people will download
 * the correct one based on the CPU architecture of their device.
 */
def enableSeparateBuildPerCPUArchitecture = false

/**
 * Run Proguard to shrink the Java bytecode in release builds.
 */
def enableProguardInReleaseBuilds = false

def Properties properties = new Properties()
properties.load(project.rootProject.file("gradle.properties.local").newDataInputStream())

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.emurgo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 52
        versionName "2.2.0"
        ndk {
          abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
        // detox setup
        testBuildType System.getProperty('testBuildType', 'debug')  // This will later be used to control the test apk build type
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    signingConfigs {
        release {
            if (project.hasProperty('MYAPP_RELEASE_STORE_FILE')) {
                storeFile file(MYAPP_RELEASE_STORE_FILE)
                storePassword properties.getProperty("MYAPP_RELEASE_STORE_PASSWORD", "")
                keyAlias MYAPP_RELEASE_KEY_ALIAS
                keyPassword properties.getProperty("MYAPP_RELEASE_KEY_PASSWORD", "")
            }
        }
    }
    splits {
        abi {
            reset()
            enable enableSeparateBuildPerCPUArchitecture
            universalApk false  // If true, also generate a universal APK
            include "armeabi-v7a", "x86", "arm64-v8a", "x86_64"
        }
    }
    buildTypes {
        release {
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }
    // Specifies one flavor dimension.
    flavorDimensions "version"
    productFlavors {
        mainnet {
            dimension "version"
            applicationId 'com.emurgo'
            minSdkVersion 21
            compileSdkVersion = 29
            targetSdkVersion 29
            resValue "string", "build_config_package", "com.emurgo"
        }
        dev {
            dimension "version"
            applicationIdSuffix ".staging"
            applicationId 'com.emurgo'
            minSdkVersion 21
            compileSdkVersion = 29
            targetSdkVersion 29
            resValue "string", "build_config_package", "com.emurgo"
        }
        nightly {
            dimension "version"
            applicationIdSuffix ".nightly"
            applicationId 'com.emurgo'
            minSdkVersion 21
            compileSdkVersion = 29
            targetSdkVersion 29
            resValue "string", "build_config_package", "com.emurgo"
        }
    }
    // applicationVariants are e.g. debug, release
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // For each separate APK per architecture, set a unique version code as described here:
            // http://tools.android.com/tech-docs/new-build-system/user-guide/apk-splits
            def versionCodes = ["armeabi-v7a":1, "x86":2, "arm64-v8a":3, "x86_64":4]
            def abi = output.getFilter(OutputFile.ABI)
            if (abi != null) {  // null for the universal-debug, universal-release variants
                output.versionCodeOverride =
                        versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }
        }
    }
    // jsc-android
    packagingOptions {
        pickFirst '**/libjsc.so'
        pickFirst '**/libc++_shared.so'
    }
}

dependencies {
    implementation "org.webkit:android-jsc:r241213"
    implementation project(':react-native-ble-plx')
    implementation project(':react-native-chain-libs')
    implementation project(':react-native-device-info')
    implementation project(':@react-native-community_async-storage')
    implementation project(':react-native-cardano')
    implementation project(':react-native-sentry')
    implementation project(':react-native-local-resource')
    implementation project(':react-native-config')
    implementation project(':react-native-splash-screen')
    implementation project(':react-native-camera')
    implementation project(':react-native-randombytes')
    implementation project(':react-native-fs')
    implementation project(':react-native-linear-gradient')
    implementation project(':react-native-svg')
    implementation project(':react-native-webview')
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation "com.facebook.react:react-native:+"  // From node_modules

    implementation project(':react-native-background-timer')
    implementation "com.google.android.gms:play-services-base:15.0.1"

    // detox setup
    androidTestImplementation('com.wix:detox:+') {
        transitive = true
        exclude group: 'com.facebook.react.module.annotations', module: 'ReactModule'
    }
    androidTestImplementation 'junit:junit:4.12'
}

// Run this once to be able to run the application with BUCK
// puts all compile dependencies into folder libs for BUCK to use
task copyDownloadableDepsToLibs(type: Copy) {
    from configurations.compile
    into 'libs'
}

tasks.create('getCurrentCommitHash') {
    doFirst {
        exec {
            executable "../../get_commit.sh"
        }
    }
}

tasks.create('checkEnvVariables') {
  doFirst {
    if (project.env.get("SHOW_INIT_DEBUG_SCREEN") == "true" || project.env.get("PREFILL_WALLET_INFO") == "true") {
      throw new GradleException("should not allow debug flags for release builds")
    }
  }
}

tasks.whenTaskAdded { task ->
    if(task.name.startsWith("bundleMainnetReleaseJsAndAssets") || task.name.startsWith("bundleNightly")) {
      task.dependsOn checkEnvVariables
    }
    if (task.name.startsWith('install') || task.name.startsWith('assemble')) {
        task.dependsOn getCurrentCommitHash
    }
}

apply from: project(':react-native-config').projectDir.getPath() + "/dotenv.gradle"
